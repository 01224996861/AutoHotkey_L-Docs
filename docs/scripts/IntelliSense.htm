<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN"
"http://www.w3.org/TR/html4/loose.dtd">
<html>
<head>
<title>IntelliSense -- by Rajat (requires XP/2k/NT)</title>
<meta http-equiv="Content-Type" content="text/html; charset=iso-8859-1">
<link rel="alternate" type="application/rss+xml" title="AutoHotkey Forum RSS" href="/forum/rss.php">
<link href="/docs/css/default.css" rel="stylesheet" type="text/css">
<link href="/docs/css/print.css" rel="stylesheet" type="text/css" media="print">
</head>
<body>

<h6>IntelliSense -- by Rajat (requires XP/2k/NT)</h6>

<p> This script watches while you edit an AutoHotkey script.  When it sees you
 type a command followed by a comma or space, it displays that command's
 parameter list to guide you.  In addition, you can press Ctrl+F1 (or
 another hotkey of your choice) to display that command's page in the help
 file. To dismiss the parameter list, press Escape or Enter.
</p>
<p><a href="IntelliSense.ahk">Download This Script</a> &nbsp;| &nbsp;<a href="index.htm">Other Sample Scripts</a> &nbsp;| &nbsp;<a href="http://www.autohotkey.com">Home</a></p>
<hr>
<pre class="NoIndent"><span class="CodeCom">; Requires v1.0.41+</span>

<span class="CodeCom">; CONFIGURATION SECTION: Customize the script with the following variables.</span>

<span class="CodeCom">; The hotkey below is pressed to display the current command's page in the</span>
<span class="CodeCom">; help file:</span>
I_HelpHotkey = ^F1

<span class="CodeCom">; The string below must exist somewhere in the active window's title for</span>
<span class="CodeCom">; IntelliSense to be in effect while you're typing.  Make it blank to have</span>
<span class="CodeCom">; IntelliSense operate in all windows.  Make it Pad to have it operate in</span>
<span class="CodeCom">; editors such as Metapad, Notepad, and Textpad.  Make it .ahk to have it</span>
<span class="CodeCom">; operate only when a .ahk file is open in Notepad, Metapad, etc.</span>
I_Editor = pad

<span class="CodeCom">; If you wish to have a different icon for this script to distinguish it from</span>
<span class="CodeCom">; other scripts in the tray, provide the filename below (leave blank to have</span>
<span class="CodeCom">; no icon). For example: E:\stuff\Pics\icons\GeoIcons\Information.ico</span>
I_Icon = 

<span class="CodeCom">; END OF CONFIGURATION SECTION (do not make changes below this point unless</span>
<span class="CodeCom">; you want to change the basic functionality of the script).</span>

SetKeyDelay, 0
#SingleInstance

if I_HelpHotkey &lt;&gt;
    Hotkey, %I_HelpHotkey%, I_HelpHotkey

<span class="CodeCom">; Change tray icon (if one was specified in the configuration section above):</span>
if I_Icon &lt;&gt;
    IfExist, %I_Icon%
        Menu, Tray, Icon, %I_Icon%

<span class="CodeCom">; Determine AutoHotkey's location:</span>
RegRead, ahk_dir, HKEY_LOCAL_MACHINE, SOFTWARE\AutoHotkey, InstallDir
if ErrorLevel  <span class="CodeCom">; Not found, so look for it in some other common locations.</span>
{
    if A_AhkPath
        SplitPath, A_AhkPath,, ahk_dir
    else IfExist ..\..\AutoHotkey.chm
        ahk_dir = ..\..
    else IfExist %A_ProgramFiles%\AutoHotkey\AutoHotkey.chm
        ahk_dir = %A_ProgramFiles%\AutoHotkey
    else
    {
        MsgBox Could not find the AutoHotkey folder.
        ExitApp
    }
}

ahk_help_file = %ahk_dir%\AutoHotkey.chm

<span class="CodeCom">; Read command syntaxes:</span>
Loop, Read, %ahk_dir%\Extras\Editors\Syntax\Commands.txt
{
    I_FullCmd = %A_LoopReadLine%

    <span class="CodeCom">; Directives have a first space instead of a first comma.</span>
    <span class="CodeCom">; So use whichever comes first as the end of the command name:</span>
    StringGetPos, I_cPos, I_FullCmd, `,
    StringGetPos, I_sPos, I_FullCmd, %A_Space%
    if (I_cPos = -1 or (I_cPos &gt; I_sPos and I_sPos &lt;&gt; -1))
        I_EndPos := I_sPos
    else
        I_EndPos := I_cPos

    if I_EndPos &lt;&gt; -1
        StringLeft, I_CurrCmd, I_FullCmd, %I_EndPos%
    else  <span class="CodeCom">; This is a directive/command with no parameters.</span>
        I_CurrCmd = %A_LoopReadLine%
    
    StringReplace, I_CurrCmd, I_CurrCmd, [,, All
    StringReplace, I_CurrCmd, I_CurrCmd, %A_Space%,, All
    StringReplace, I_FullCmd, I_FullCmd, ``n, `n, All
    StringReplace, I_FullCmd, I_FullCmd, ``t, `t, All
    
    <span class="CodeCom">; Make arrays of command names and full cmd syntaxes:</span>
    I_Cmd%A_Index% = %I_CurrCmd%
    I_FullCmd%A_Index% = %I_FullCmd%
}

<span class="CodeCom">; Use the Input command to watch for commands that the user types:</span>
Loop
{
    <span class="CodeCom">; Editor window check:</span>
    WinGetTitle, ActiveTitle, A
    IfNotInString, ActiveTitle, %I_Editor%
    {
        ToolTip
        Sleep, 500
        Continue
    }
    
    <span class="CodeCom">; Get all keys till endkey:</span>
    Input, I_Word, V, {enter}{escape}{space}`,
    I_EndKey = %ErrorLevel%
    
    <span class="CodeCom">; Tooltip is hidden in these cases:</span>
    if I_EndKey in EndKey:Enter,EndKey:Escape
    {
        ToolTip
        Continue
    }

    <span class="CodeCom">; Editor window check again!</span>
    WinGetActiveTitle, ActiveTitle
    IfNotInString, ActiveTitle, %I_Editor%
    {
        ToolTip
        Continue
    }

    <span class="CodeCom">; Compensate for any indentation that is present:</span>
    StringReplace, I_Word, I_Word, %A_Space%,, All
    StringReplace, I_Word, I_Word, %A_Tab%,, All
    if I_Word =
        Continue
    
    <span class="CodeCom">; Check for commented line:</span>
    StringLeft, I_Check, I_Word, 1
    if (I_Check = &quot;;&quot; or I_Word = &quot;If&quot;)  <span class="CodeCom">; &quot;If&quot; seems a little too annoying to show tooltip for.</span>
        Continue

    <span class="CodeCom">; Match word with command:</span>
    I_Index =
    Loop
    {
        <span class="CodeCom">; It helps performance to resolve dynamic variables only once.</span>
        <span class="CodeCom">; In addition, the value put into I_ThisCmd is also used by the</span>
        <span class="CodeCom">; I_HelpHotkey subroutine:</span>
        I_ThisCmd := I_Cmd%A_Index%
        if I_ThisCmd =
            break
        if (I_Word = I_ThisCmd)
        {
            I_Index := A_Index
            I_HelpOn = %I_ThisCmd%
            break
        }
    }
    
    <span class="CodeCom">; If no match then resume watching user input:</span>
    if I_Index =
        Continue
    
    <span class="CodeCom">; Show matched command to guide the user:</span>
    I_ThisFullCmd := I_FullCmd%I_Index%
    ToolTip, %I_ThisFullCmd%, A_CaretX, A_CaretY + 20
}



I_HelpHotkey:
WinGetTitle, ActiveTitle, A
IfNotInString, ActiveTitle, %I_Editor%, Return

ToolTip  <span class="CodeCom">; Turn off syntax helper since there is no need for it now.</span>

SetTitleMatchMode, 1  <span class="CodeCom">; In case it's 3. This setting is in effect only for this thread.</span>
IfWinNotExist, AutoHotkey Help
{
    IfNotExist, %ahk_help_file%
    {
        MsgBox, Could not find the help file: %ahk_help_file%.
        return
    }
    Run, %ahk_help_file%
    WinWait, AutoHotkey Help
}

if I_ThisCmd =  <span class="CodeCom">; Instead, use what was most recently typed.</span>
    I_ThisCmd := I_Word

<span class="CodeCom">; The above has set the &quot;last found&quot; window which we use below:</span>
WinActivate
WinWaitActive
StringReplace, I_ThisCmd, I_ThisCmd, #, {#}  <span class="CodeCom">; Replace leading #, if any.</span>
Send, !n{home}+{end}%I_HelpOn%{enter}
return
</pre>
</body>
</html>
