<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN"
"http://www.w3.org/TR/html4/loose.dtd">
<html>
<head>
<title>ToolTip Mouse Menu (requires XP/2k/NT) -- by Rajat</title>
<meta http-equiv="Content-Type" content="text/html; charset=iso-8859-1">
<link rel="alternate" type="application/rss+xml" title="AutoHotkey Forum RSS" href="/forum/rss.php">
<link href="/docs/css/default.css" rel="stylesheet" type="text/css">
<link href="/docs/css/print.css" rel="stylesheet" type="text/css" media="print">
</head>
<body>

<h6>ToolTip Mouse Menu (requires XP/2k/NT) -- by Rajat</h6>

<p> This script displays a popup menu in response to briefly holding down
 the middle mouse button.  Select a menu item by left-clicking it.
 Cancel the menu by left-clicking outside of it.  A recent improvement
 is that the contents of the menu can change depending on which type of
 window is active (Notepad and Word are used as examples here).
</p>
<p><a href="TooltipMouseMenu.ahk">Download This Script</a> &nbsp;| &nbsp;<a href="index.htm">Other Sample Scripts</a> &nbsp;| &nbsp;<a href="http://www.autohotkey.com">Home</a></p>
<hr>
<pre class="NoIndent"><span class="CodeCom">; You can set any title here for the menu:</span>
MenuTitle = -=-=-=-=-=-=-=-

<span class="CodeCom">; This is how long the mouse button must be held to cause the menu to appear:</span>
UMDelay = 20

SetFormat, float, 0.0
SetBatchLines, 10ms 
SetTitleMatchMode, 2
#SingleInstance


<span class="CodeCom">;___________________________________________</span>
<span class="CodeCom">;_____Menu Definitions______________________</span>

<span class="CodeCom">; Create / Edit Menu Items here.</span>
<span class="CodeCom">; You can't use spaces in keys/values/section names.</span>

<span class="CodeCom">; Don't worry about the order, the menu will be sorted.</span>

MenuItems = Notepad/Calculator/Section 3/Section 4/Section 5


<span class="CodeCom">;___________________________________________</span>
<span class="CodeCom">;______Dynamic menuitems here_______________</span>

<span class="CodeCom">; Syntax:</span>
<span class="CodeCom">;     Dyn# = MenuItem|Window title</span>

Dyn1 = MS Word|- Microsoft Word
Dyn2 = Notepad II|- Notepad

<span class="CodeCom">;___________________________________________</span>

Exit


<span class="CodeCom">;___________________________________________</span>
<span class="CodeCom">;_____Menu Sections_________________________</span>

<span class="CodeCom">; Create / Edit Menu Sections here.</span>

Notepad:
Run, Notepad.exe
Return

Calculator:
Run, Calc
Return

Section3:
MsgBox, You selected 3
Return

Section4:
MsgBox, You selected 4
Return

Section5:
MsgBox, You selected 5
Return

MSWord:
msgbox, this is a dynamic entry (word)
Return

NotepadII:
msgbox, this is a dynamic entry (notepad)
Return


<span class="CodeCom">;___________________________________________</span>
<span class="CodeCom">;_____Hotkey Section________________________</span>

~MButton::
HowLong = 0
Loop
{
    HowLong ++
    Sleep, 10
    GetKeyState, MButton, MButton, P
    IfEqual, MButton, U, Break
}
IfLess, HowLong, %UMDelay%, Return


<span class="CodeCom">;prepares dynamic menu</span>
DynMenu =
Loop
{
    IfEqual, Dyn%a_index%,, Break

    StringGetPos, ppos, dyn%a_index%, |
    StringLeft, item, dyn%a_index%, %ppos%
    ppos += 2
    StringMid, win, dyn%a_index%, %ppos%, 1000

    IfWinActive, %win%,
        DynMenu = %DynMenu%/%item%
}


<span class="CodeCom">;Joins sorted main menu and dynamic menu</span>
Sort, MenuItems, D/
TempMenu = %MenuItems%%DynMenu%


<span class="CodeCom">;clears earlier entries</span>
Loop
{
    IfEqual, MenuItem%a_index%,, Break
    MenuItem%a_index% =
}

<span class="CodeCom">;creates new entries</span>
Loop, Parse, TempMenu, /
{
    MenuItem%a_index% = %a_loopfield%
}

<span class="CodeCom">;creates the menu</span>
Menu = %MenuTitle%
Loop
{
    IfEqual, MenuItem%a_index%,, Break
    numItems ++
    StringTrimLeft, MenuText, MenuItem%a_index%, 0
    Menu = %Menu%`n%MenuText%
}

MouseGetPos, mX, mY
HotKey, ~LButton, MenuClick
HotKey, ~LButton, On
ToolTip, %Menu%, %mX%, %mY%
WinActivate, %MenuTitle%
Return


MenuClick:
HotKey, ~LButton, Off
IfWinNotActive, %MenuTitle%
{
    ToolTip
    Return
}

MouseGetPos, mX, mY
ToolTip
mY -= 3        <span class="CodeCom">;space after which first line starts</span>
mY /= 13    <span class="CodeCom">;space taken by each line</span>
IfLess, mY, 1, Return
IfGreater, mY, %numItems%, Return
StringTrimLeft, TargetSection, MenuItem%mY%, 0
StringReplace, TargetSection, TargetSection, %a_space%,, A
Gosub, %TargetSection%
Return
</pre>
</body>
</html>
