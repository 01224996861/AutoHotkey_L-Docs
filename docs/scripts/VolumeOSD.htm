<!DOCTYPE HTML>
<html lang="en">
<head>
<title>Volume On-Screen-Display - Script Example | AutoHotkey v2</title>
<meta name="description" content="The volume on-screen-display (OSD) script allows pressing hotkeys to raise and lower the master volume." />
<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
<meta http-equiv="X-UA-Compatible" content="IE=edge">
<link href="../static/theme.css" rel="stylesheet" type="text/css" />
<script src="../static/content.js" type="text/javascript"></script>
</head>
<body>

<h1>Volume On-Screen-Display <span class="headnote">Based on the v1 script by Rajat</span></h1>

<p>This script assigns hotkeys of your choice to raise and lower the
 master volume.
</p>
<p><a href="VolumeOSD.ahk">Download This Script</a> &nbsp;| &nbsp;<a href="index.htm">Other Sample Scripts</a> &nbsp;| &nbsp;<a href="../AutoHotkey.htm">Home</a></p>

<pre class="NoIndent"><em>; --- User Settings ---</em>

global config := {}

<em>; The percentage by which to raise or lower the volume each time:</em>
config.Step := 4

<em>; How long to display the volume level bar graphs:</em>
config.DisplayTime := 2000

<em>; Master Volume Bar color (see the help file to use more precise shades):</em>
config.CBM := "Red"

<em>; Background color:</em>
config.CW := "Silver"

<em>; Bar's screen position. Use "center" to center the bar in that dimension:</em>
config.PosX := "center"
config.PosY := "center"
config.Width := 150  <em>; width of bar</em>
config.Thick := 12   <em>; thickness of bar</em>

<em>; If your keyboard has multimedia buttons for Volume, you can
; try changing the below hotkeys to use them by specifying
; Volume_Up and Volume_Down:</em>
config.MasterUp := "#Up"      <em>; Win+UpArrow</em>
config.MasterDown := "#Down"

<em>; --- Auto Execute Section ---

; DON'T CHANGE ANYTHING HERE (unless you know what you're doing).</em>

#SingleInstance

<em>; Create the Progress window:</em>
global G := GuiCreate("+ToolWindow -Caption -Border +Disabled")
G.MarginX := 0, G.MarginY := 0
opt := "w" config.Width " h" config.Thick " background" config.CW
G.Add("Progress", opt " vMaster c" config.CBM)

<em>; Register hotkeys:</em>
Hotkey config.MasterUp,   () =&gt; ChangeVolume("+")
Hotkey config.MasterDown, () =&gt; ChangeVolume("-")

<em>; --- Function Definitions ---</em>

ChangeVolume(Prefix)
{
    SoundSetVolume(Prefix config.Step)
    G["Master"].Value := Round(SoundGetVolume())
    G.Show("x" config.PosX " y" config.PosY)
    SetTimer "HideWindow", -config.DisplayTime
}

HideWindow()
{
    G.Hide()
}</pre>
</body>
</html>
